control_station(C,L_cur,L_demand,S_storage) :- c(C,i1,L_cur), c(C,i2,L_demand), c(C,i3,S_storage), control(C), not ab(C).

%calculate minimum
min(A,B,MIN) :- #int(A), #int(B), #int(MIN), A < B, MIN=A.
min(A,B,MIN) :- #int(A), #int(B), #int(MIN), A >= B, MIN=B.

%rules for activating plants
energy_needed(V) :- control_station(_,L_cur,L_demand,_), L_demand > L_cur, V = L_demand-L_cur.
no_energy_needed :- control_station(_,L_cur,L_demand,_), L_demand <= L_cur.
storage_usable :- control_station(_,_,_,S_storage), S_storage != empty.
storage_not_usable :- control_station(_,_,_,S_storage), S_storage = empty.
windspeed_ok :- control_station(_,_,_,_), S_wind <= 40 , S_wind >= 5, c(C,i4,S_wind).
windspeed_not_ok :- control_station(_,_,_,_), S_wind > 40, c(C,i4,S_wind).
windspeed_not_ok :- control_station(_,_,_,_), S_wind < 5, c(C,i4,S_wind).

%set switching to output
p(C,o4,1) :- energy_needed(V), control_station(C,_,_,_).
p(C,o5,0) :- energy_needed(V), control_station(C,_,_,_).

%windmill activation
activate_windmill_with(OUT) :- windspeed_ok, energy_needed(V), windmill(W), c_max(W,C_w), min(C_w,V,OUT).

windmill_not_active :- windspeed_not_ok.
windmill_not_active :- no_energy_needed.


%storage activation
%windmill is active but we still need energy
activate_storage_with(OUT) :- storage_usable, activate_windmill_with(E), energy_needed(V), E < V, storage_plant(PSH), c_max(PSH,C_psh),SN = V-E, min(C_psh,SN,OUT).

%windmill can not be used
activate_storage_with(OUT) :- windspeed_not_ok, storage_usable, energy_needed(V), storage_plant(PSH), c_max(PSH,C_psh), min(C_psh,V,OUT).

storage_not_active :- storage_not_usable.
storage_not_active :- no_energy_needed.
%windmill produces enough on its own
storage_not_active :- activate_windmill_with(E) , energy_needed(V), E >= V.


%caloric activation

%define both others are active
storage_and_windmill_produce(OUT) :- activate_windmill_with(A), activate_storage_with(B), OUT = A+B.

%both others are on but do not suffice
activate_caloric_with(OUT) :- storage_and_windmill_produce(E), energy_needed(V), caloric_plant(F), c_max(F,C_f), E < V, SN = V-E, min(C_f,SN,OUT).
%only windmill active, storage unusable and insufficient
activate_caloric_with(OUT) :- storage_not_usable, activate_windmill_with(E), energy_needed(V), caloric_plant(F), c_max(F,C_f), E < V, SN = V-E, min(C_f,SN,OUT).
%only psh active, windmill unusable and insufficient
activate_caloric_with(OUT) :- windspeed_not_ok, activate_storage_with(E), energy_needed(V), caloric_plant(F), c_max(F,C_f), E < V, SN = V-E, min(C_f,SN,OUT).
%nothing else active
activate_caloric_with(OUT) :- windspeed_not_ok, storage_not_usable, energy_needed(V), caloric_plant(F), c_max(F,C_f), min(V,C_f,OUT).



caloric_not_active :- no_energy_needed.
%storage and windmill produce enough energy
caloric_not_active :- storage_and_windmill_produce(OUT), energy_needed(V), OUT >= V.
%storage produce enough energy
caloric_not_active :- activate_storage_with(E) , energy_needed(V), E >= V.
%windmill produce enough energy
caloric_not_active :- activate_windmill_with(E) , energy_needed(V), E >= V.


p(C,o2,OUT) :- control_station(C,_,_,_),activate_windmill_with(OUT). 
p(C,o1,OUT) :- control_station(C,_,_,_),activate_storage_with(OUT).
p(C,o3,OUT) :- control_station(C,_,_,_),activate_caloric_with(OUT).

%set outputs to 0 if a station is not activated
p(C,o2,0) :- control_station(C,_,_,_), windmill_not_active. 
p(C,o1,0) :- control_station(C,L_cur,L_demand,_), storage_not_active, L_cur < L_demand.
p(C,o3,0) :- control_station(C,_,_,_), caloric_not_active.


%set to charge
p(C,o1,charge) :- control_station(C,L_cur,L_demand,S_storage), L_cur > L_demand, S_storage != full.
p(C,o4,2) :- control_station(C,L_cur,L_demand,S_storage), L_cur > L_demand, S_storage != full.
p(C,o5,P) :- control_station(C,L_cur,L_demand,S_storage), L_cur > L_demand, S_storage != full, min(DIF,V,P), DIF = L_cur-L_demand, w_charge(V).

p(C,o1,0) :- control_station(C,L_cur,L_demand,S_storage), L_cur > L_demand, S_storage = full.
p(C,o4,0) :- control_station(C,L_cur,L_demand,S_storage), L_cur > L_demand, S_storage = full.
p(C,o5,0) :- control_station(C,L_cur,L_demand,S_storage), L_cur > L_demand, S_storage = full.


%all other cases since we covered Lc><Ld only equals remains
p(C,o1,0):- control_station(C,L_cur,L_demand,_), L_cur = L_demand.
p(C,o2,0):- control_station(C,L_cur,L_demand,_), L_cur = L_demand.
p(C,o3,0):- control_station(C,L_cur,L_demand,_), L_cur = L_demand.
p(C,o4,0):- control_station(C,L_cur,L_demand,_), L_cur = L_demand.
p(C,o5,0):- control_station(C,L_cur,L_demand,_), L_cur = L_demand.
